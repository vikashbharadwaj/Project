plugins {
    id 'java'
}

group = 'org.example'
version = '1.0-SNAPSHOT'
configurations {
    reverseMap
}
repositories {
    mavenCentral()
}

project.ext {
    hibernateDestDir = file("$projectDir/src/main/java")
}

dependencies {
    reverseMap 'org.hibernate:hibernate-core:4.0.1.Final'
    reverseMap 'org.hibernate:hibernate-tools:4.0.1.Final'
    reverseMap 'org.slf4j:slf4j-simple:1.7.5'
    reverseMap 'mysql:mysql-connector-java:5.1.48'
    implementation 'com.mysql:mysql-connector-j:8.0.32'
    implementation 'org.hibernate:hibernate-java8:5.4.18.Final'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

task reverseMap {
    outputs.dir hibernateDestDir
    doLast {
        hibernateDestDir.exists() || hibernateDestDir.mkdirs()
        ant {
            taskdef(
                    name: 'hibernatetool',
                    classname: 'org.hibernate.tool.ant.HibernateToolTask',
                    classpath: configurations.reverseMap.asPath
            )
            hibernatetool(destdir: hibernateDestDir) {
                jdbcconfiguration(
                        configurationfile: "$projectDir/db/hibernate.cfg.xml",
                        packagename: "com.me.models"
                )
                hbm2java(
                        jdk5: true,
                        ejb3: true
                )
            }
        }
    }
}